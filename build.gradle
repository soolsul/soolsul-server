buildscript {
    ext {
        queryDslVersion = "5.0.0"
    }
}

plugins {
    id 'org.springframework.boot' version '2.7.5'
    id 'io.spring.dependency-management' version '1.0.15.RELEASE'
    id "com.ewerk.gradle.plugins.querydsl" version "1.0.10"
    id 'org.asciidoctor.convert' version '1.5.8'
    id 'java'
    id 'jacoco'
    id "org.sonarqube" version "3.4.0.2513"
}

group = 'com.soolsul'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

ext {
    set('snippetsDir', file("build/generated-snippets"))
}

dependencies {
    // Web
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springdoc:springdoc-openapi-ui:1.6.7'

    // Healt Check
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator', version: '2.7.0'

    // DB
    implementation "com.querydsl:querydsl-jpa:${queryDslVersion}"
    implementation 'org.projectlombok:lombok:1.18.20'
    annotationProcessor "com.querydsl:querydsl-apt:${queryDslVersion}"
    runtimeOnly 'com.mysql:mysql-connector-j'
    implementation 'com.github.gavlyukovskiy:p6spy-spring-boot-starter:1.8.1'

    // redis
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'

    // DevTool
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // Client
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign:3.1.3'

    // WireMock
    testImplementation 'org.springframework.cloud:spring-cloud-starter-contract-stub-runner:3.1.5'


    // JWT
    compile 'io.jsonwebtoken:jjwt-api:0.11.5'
    implementation 'io.jsonwebtoken:jjwt-impl:0.11.5',
            // Uncomment the next line if you want to use RSASSA-PSS (PS256, PS384, PS512) algorithms:
            //'org.bouncycastle:bcprov-jdk15on:1.70',
            'io.jsonwebtoken:jjwt-jackson:0.11.5' // or 'io.jsonwebtoken:jjwt-gson:0.11.5' for gson

    // API Docs
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'

    // Test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'io.rest-assured:rest-assured:4.2.0'

    // Test Container
    testImplementation "org.junit.jupiter:junit-jupiter:5.8.1"
    testImplementation "org.testcontainers:testcontainers:1.17.6"
    testImplementation "org.testcontainers:junit-jupiter:1.17.6"
    testImplementation "org.testcontainers:mysql:1.17.6"

    //aws sdk
    implementation 'com.amazonaws:aws-java-sdk-s3:1.12.332'
    testImplementation 'org.testcontainers:localstack:1.17.4'
}

tasks.named('test') {
    outputs.dir snippetsDir
    useJUnitPlatform()
}

tasks.named('asciidoctor') {
    inputs.dir snippetsDir
    dependsOn test
}

//querydsl
def querydslDir = "$buildDir/generated/querydsl"
querydsl {
    jpa = true
    querydslSourcesDir = querydslDir
}
sourceSets {
    main.java.srcDir querydslDir
}
configurations {
    querydsl.extendsFrom compileClasspath
}
compileQuerydsl {
    options.annotationProcessorPath = configurations.querydsl
}

//jacoco
test {
    jacoco {
        destinationFile = file("$buildDir/jacoco/jacoco.exec")
    }
    useJUnitPlatform()
    finalizedBy 'jacocoTestReport'
}


jacoco {
    // jacoco version
    toolVersion = '0.8.8'
}

jacocoTestReport {
    dependsOn test
    reports {
        html.enabled true // 개발자용 html
        xml.enabled true // 소나큐브용
        csv.enabled true

        html.destination file("src/jacoco/jacoco.html")
    }

    // QueryDSL은 분석에서 제외시켜야 함
    def Qdomains = []
    for (qPattern in '**/QA'..'**/QZ') {
        Qdomains.add(qPattern + '*')
    }

    afterEvaluate {
        classDirectories.setFrom(
                files(classDirectories.files.collect {
                    fileTree(dir: it, excludes: [
                            '**/*Request.*',
                            '**/*Response.*',
                            '**/dto/**',
                            '**/*Interceptor.*',
                            '**/*Exception.*',
                            '**/*Storage.*',
                            '**/SoolsulServerApplication.*',
                            '**/*Token.*'
                    ] + Qdomains)
                })
        )
    }

    finalizedBy 'jacocoTestCoverageVerification'
}

jacocoTestCoverageVerification {
    def Qdomains = []
    for (qPattern in '*.QA'..'*.QZ') {
        Qdomains.add(qPattern + '*')
    }

    violationRules {
        rule {
            limit {
                element = 'CLASS'
                minimum = 0.0
            }

            excludes = [
                    '**.*Request.*',
                    '**.*Response.*',
                    '**/dto/**',
                    '**.*Interceptor.*',
                    '**.*Exception.*',
                    '**.*Storage.*',
                    '**.SoolsulServerApplication.*',
                    '**.*Token.*'
            ] + Qdomains
        }
    }
}

task testCoverage(type: Test) {
    group 'verification'
    description 'Runs the unit tests with coverage'

    dependsOn(':test',
            ':jacocoTestReport',
            ':jacocoTestCoverageVerification')

    tasks['jacocoTestReport'].mustRunAfter(tasks['test'])
    tasks['jacocoTestCoverageVerification'].mustRunAfter(tasks['jacocoTestReport'])
}

sonarqube {
    properties {
        property "sonar.projectKey", "project key"
    }
}
